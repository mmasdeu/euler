import tactic
import analysis.special_functions.trigonometric
import measure_theory.interval_integral
import topology.basic
import data.finset
import .integrals

noncomputable theory
open_locale classical
open_locale big_operators
open interval_integral
open filter
open real

open_locale topological_space

/-!
#### The proof of Euler summation : ‚àë 1/n^2 = œÄ^2/6

## Strategy

1. Define sequences

  A‚Çô = ‚à´ x in 0..œÄ/2 (cos x)^(2*n) and
  B‚Çô = ‚à´ x in 0..œÄ/2 x^2 * (cos x)^(2*n).

2. Use integration by parts to prove recurrence formulas

  A‚Çô‚Çä‚ÇÅ = (2 * n + 1) * (n+1) * B‚Çô - 2*(n+1)^2 * B‚Çô‚Çä‚ÇÅ
    and
  A‚Çô‚Çä‚ÇÅ = (2*n + 1) * (A‚Çô - A‚Çô‚Çä‚ÇÅ)

3. Express 1/((n+1)^2) in terms of two consecutive ratios:

  1 / ((n +1)^2 = 2 * (B‚Çô) / (A‚Çô) - 2 * B‚Çô‚Çä‚ÇÅ / A‚Çô‚Çä‚ÇÅ

4. The partial sums telescope and yield

  ‚àë k=0..(n-1) 1 / ((k+1)^2 =  2 * B‚ÇÄ / A‚ÇÄ - 2 * B‚Çô/A‚Çô = œÄ^2 / 6 - 2 * B‚Çô/A‚Çô

5.  Bound the error term using the fact that

  2/œÄ * x ‚â§ sin x.

## References

Daniel Daners,
A short elementary proof of ...
Mathematics Magazine 85 (2012), 361-364. (MR 3007217, Zbl 1274.97037)

* <http://talus.maths.usyd.edu.au/u/daners/publ/abstracts/zeta2/>

## Tags

euler summation, number theory, reciprocals

-/

def A : ‚Ñï ‚Üí ‚Ñù := Œª n, ‚à´ x in 0..real.pi/2, (cos x)^(2*n)
def B : ‚Ñï ‚Üí ‚Ñù := Œª n, ‚à´ x in 0..real.pi/2, x^2 * (cos x)^(2*n)

/-
Evaluate A 0 and B 0, which will be useful later
-/
lemma eval_A0 : A 0 = real.pi / (2 : ‚Ñù) :=
begin
  unfold A,
  simp only [mul_zero, pow_zero],
  suffices : ‚à´ (x : ‚Ñù) in 0..real.pi / 2, (1:‚Ñù) = (real.pi / 2 - 0) ‚Ä¢ 1,
  {
    rw this,
    simp only [mul_one, algebra.id.smul_eq_mul, sub_zero],
  },
  apply interval_integral.integral_const,
end

lemma has_deriv_at_congr {f : ‚Ñù ‚Üí ‚Ñù} {f' g' : ‚Ñù} (x : ‚Ñù) (h: f' = g') :
has_deriv_at f f' x ‚Üí has_deriv_at f g' x :=
(iff_of_eq (congr_fun (congr_arg (has_deriv_at f) h) x)).1

lemma eval_B0 : B 0 = real.pi^3 / (24 : ‚Ñù) :=
begin
  unfold B,
  simp only [mul_one, mul_zero, pow_zero],
  have h : ‚àÄ x ‚àà set.interval 0 (real.pi/2), has_deriv_at (Œª (x:‚Ñù), x^3/3) (x^2) x,
  {
    intros x hx,
    have hh : (3‚Åª¬π * (3 * x ^ 2)) = x^2 := by discrete_field,
    suffices : has_deriv_at (Œª (x : ‚Ñù), x ^ 3 / 3) (3‚Åª¬π * (3 * x ^ 2)) x, by exact has_deriv_at_congr x hh this,
    simp [div_eq_mul_inv, mul_comm],
    apply has_deriv_at.const_mul,
    apply_mod_cast (has_deriv_at_pow 3 x),
  },
  rw integral_eq_sub_of_has_deriv_at h,
  { simp only [div_pow],
    ring },
  { show_continuous },
end

/-
Show that B n is positive for all n. A similar proof works to show that A n is positive,
but we decided to prove the latter one by induction, which we will do once we have
an explicit recursive formula for A n.

For B n, the proof just uses that the integrand is the square of a nonzero function.
-/
lemma B_pos {n : ‚Ñï} : 0 < B n :=
begin
  unfold B,
  have pi_pos := pi_pos,
  simp only [mul_comm, pow_mul, ‚Üêmul_pow],
  apply int_pos_of_square (real.pi/3) pi_div_two_pos,
  { show_continuous },
  { -- Show here that the integrand is nonzero at œÄ/3
    repeat {split},
    repeat {linarith},
    rw cos_pi_div_three,
    field_simp [pi_ne_zero],
  }
end

lemma first_lemma' (n : ‚Ñï) : A (n + 1)= (2*(n:‚Ñù)+1) * ‚à´ x in 0..real.pi/2, ((sin x)^2 * (cos x)^(2*n)) :=
begin
  calc
  A (n + 1) = ‚à´ x in 0..real.pi/2, (cos x)^(2*(n+1)) : by {unfold A}
  ... =  ‚à´ x in 0..real.pi/2, (cos x)^(2*n+1) * (deriv sin x) :
  begin
    congr, ext1,
    rw real.deriv_sin,
    ring_nf,
  end
  ... = ‚à´ x in 0..real.pi/2, (2*n+1) * (sin x)^2 * (cos x)^(2*n) :
  begin
    rw int_by_parts,
    {
    suffices : ‚à´ x in 0..real.pi / 2,
    sin x * ((2*n + 1) * (cos x ^ (2 * n) * sin x)) =
    ‚à´ x in 0..real.pi / 2, (2*n + 1) * sin x^2 * cos x^(2 * n), by simpa,
      congr, ext1,
      ring,
    },
    { apply differentiable.pow,
      apply differentiable.cos,
      exact differentiable_id },
    { exact differentiable_sin },
    { exact continuous_deriv_cospow (2*n) },
    { rw real.deriv_sin,
      exact continuous_cos },
  end
  ... = ‚à´ x in 0..real.pi/2, (2*(n:‚Ñù)+1) * ((sin x)^2 * (cos x)^(2*n)) : by {congr, ext1, ring}
  ... = (2*(n:‚Ñù)+1) * ‚à´ x in 0..real.pi/2, ((sin x)^2 * (cos x)^(2*n)) : by {simp [my_integral_smul]}
end

lemma first_lemma (n : ‚Ñï) : A (n + 1)  = (2*n + 1) * (A (n) - A (n+1)) :=
begin
  calc
  A (n + 1) = (2*(n:‚Ñù)+1) * ‚à´ x in 0..real.pi/2, ((sin x)^2 * (cos x)^(2*n)) : first_lemma' n
  ... = (2*(n:‚Ñù)+1) * ‚à´ x in 0..real.pi/2, (1- (cos x)^2) * (cos x)^(2*n) :
  begin
    congr, ext1,
    suffices : sin x^2 = 1 - cos x^2, rw this,
    simp only [eq_sub_iff_add_eq, sin_sq_add_cos_sq],
  end
  ... = (2*(n:‚Ñù)+1) * (A (n) - A (n+1)) :-- by {rw f5}
  begin
    unfold A,
    rw ‚Üêintegral_sub,
    { congr, discrete_field },
    all_goals {
      apply integrable_of_cont,
      apply continuous.pow continuous_cos,
    },
  end
end

lemma first_lemma_cor (n : ‚Ñï) : A (n+1) = (2 * n + 1) / (2 * n + 2) * A n :=
begin
  have h := first_lemma n,
  have h1 : 2 * (n : ‚Ñù) + 1 ‚â† 0 := by show_nonzero,
  have h2 : 2 * (n : ‚Ñù) + 2 ‚â† 0 := by show_nonzero,
  have h3 : 2 * (n : ‚Ñù) + 2 = (2 * n + 1) + 1 := by ring,
  field_simp [h1, h2],
  rw [h3, mul_add, mul_one],
  nth_rewrite_lhs 1 h,
  ring,
end

/-
The recurrence formula for A n directly gives positivity by induction.
-/
lemma A_pos {n : ‚Ñï} : 0 < A n :=
begin
  induction n with d hd,
  { rw eval_A0,
    exact pi_div_two_pos },
  { rw_mod_cast first_lemma_cor d,
    show_pos },
end
/-
-/
lemma display4 (n : ‚Ñï) :
  A (n+1) = (2 * n + 1) * (n+1) * B n - 2*(n+1)^2 * B (n+1) :=
begin
  calc
  A (n + 1) = ‚à´ x in 0..real.pi/2, (cos x)^(2*(n+1)) : by {unfold A}
  ... = ‚à´ x in 0..real.pi/2, (cos x)^(2*n+2) * ((deriv id) x) : by {discrete_field}
-- Integrate by parts
  ... = -‚à´ x in 0..real.pi/2, x * (2*n+2) * (cos x)^(2*n+1) * (deriv cos) x :
  begin
    rw int_by_parts_zero_ends,
    { congr,
      discrete_field },
    all_goals
    {
      discrete_field,
      try {show_continuous}
    },
  end
  ... = ((n:‚Ñù)+1) * ‚à´ x in 0..real.pi/2, (2*x) * sin x * (cos x)^(2*n+1) :
  begin
    rw [‚Üêmy_integral_smul, ‚Üêintegral_neg],
    congr,
    discrete_field,
  end
  ... = (n+1) * ‚à´ x in 0..real.pi/2, sin x * (cos x)^(2*n+1) * deriv (Œª x, x^2) x :
  begin
    congr, ext,
    simp only [mul_one, differentiable_at_id', deriv_pow'',
      nat.cast_bit0, deriv_id'', pow_one, nat.cast_one],
    linarith,
  end
-- Integrate by parts a second time
  ... = (n+1) * -‚à´ x in 0..real.pi/2, x^2 * (deriv (Œª x, sin x * (cos x)^(2*n+1))) x :
  begin
    rw int_by_parts_zero_ends,
    { show_differentiable },
    { exact differentiable_pow },
    {
      rw deriv_sin_cos,
      apply continuous.sub;
      exact continuous_cospow',
    },
    all_goals {
      simp only [algebra.id.smul_eq_mul, pow_one,
      nat.cast_one, power_rule'', continuous_mul_left,
      sin_zero, zero_mul,
      cos_pi_div_two, zero_mul, add_eq_zero_iff, ne.def, not_false_iff,
      one_ne_zero, mul_zero, and_false, zero_pow'],
    },
  end
  ... = (n+1) * -‚à´ x in 0..real.pi/2,
    x^2 * ((cos x)^(2*n+2) - (2*n+1) * (1 - cos x^2) * (cos x)^(2*n)) :
  begin
    congr, ext, congr,
    discrete_field,
  end
  ... = (n+1) * ((2 *n + 1) * B n - 2*(n+1) * B (n+1)) :
  begin
    congr,
    unfold B,
    rw ‚Üêintegral_neg,
    repeat {rw_mod_cast ‚Üêmy_integral_smul,},
    rw ‚Üêintegral_sub,
    {
      congr, ext,
      simp only [nat.cast_bit0, nat.cast_add, nat.cast_one, nat.cast_mul],
      ring_exp,
    },
    all_goals {
      apply integrable_of_cont,
      show_continuous,
    },
  end
  ... = (2 * n + 1) * (n+1) * B n - 2*(n+1)^2 * B (n+1) : by {ring}
end

lemma summand_expression (n : ‚Ñï) :
  1 / ((n : ‚Ñù) + 1)^2 = 2 * (B n) / (A n) - 2 * B (n+1) / A (n+1) :=
begin
  have A_nonzero : ‚àÄ (m:‚Ñï), A m ‚â† 0,
  {
    intro m,
    apply norm_num.ne_zero_of_pos,
    exact A_pos,
  },
  have nplusone_nonzero : (n:‚Ñù)+1 ‚â† 0 := nat.cast_add_one_ne_zero n,
  have twonplusone_nonzero : 2*(n:‚Ñù)+1 ‚â† 0,
    show_nonzero,
  have h_first_lemma := first_lemma n,
  calc
  1 / ((n : ‚Ñù) + 1)^2 = (A (n+1)) / (A (n+1) * ((n : ‚Ñù) + 1)^2) :
  begin
    rw div_mul_right,
    exact A_nonzero (n+1),
  end
  ... = ((2 * n + 1) * (n+1) * (B n) - 2*(n+1)^2 * (B (n+1))) / (A (n+1) * ((n : ‚Ñù) + 1)^2) : 
    by {nth_rewrite 0 display4,}
  ... = ((2 * n + 1) * (n+1) * (B n)) / (A (n+1) * ((n : ‚Ñù) + 1)^2) -
    (2*(n+1)^2 * (B (n+1))) / (A (n+1) * ((n : ‚Ñù) + 1)^2) :
    by {rw sub_div}
  ... = ((2 * n + 1) * (B n)) / (A (n+1) * ((n : ‚Ñù) + 1)) -
    2 * (B (n+1)) / (A (n+1)) : by {field_simp *, ring}
  ... = 2 * (B n) / (A n) - 2 * B (n+1) / A (n+1) :
  begin
    have : (A (n+1) * ((n:‚Ñù) + 1)) = (2*n + 1) / 2 * (A n) := by discrete_field,
    rw this,
    discrete_field,
  end
end

lemma telescoping (n : ‚Ñï) : ‚àë k in (finset.range n), (1 : ‚Ñù) / ((k+1)^2) = 2 * B 0 / A 0 - 2 * B n / A n :=
begin
  simp only [summand_expression],
  exact finset.sum_range_sub' (Œª k, 2 * (B k) / (A k)) n,
end

/-
The sin function is concave on the interval [0..pi/2].
-/
lemma sin_is_concave : concave_on (set.Icc 0 (real.pi/2)) sin :=
begin
  have h0 : -sin = Œª y, -sin y := by refl,
  rw ‚Üêneg_convex_on_iff,
  apply convex_on_of_deriv2_nonneg (convex_Icc 0 (real.pi / 2)),
  { show_continuous },
  { show_differentiable },
  {
    simp only [h0],
    show_differentiable,
  },
  {
    intros x hx,
    replace hx : 0 ‚â§ x ‚àß x ‚â§ real.pi / 2 := set.mem_Icc.mp (interior_subset hx),
    suffices : 0 ‚â§ deriv (deriv (-sin)) x, by simpa,
    simp only [h0],
    suffices : 0 ‚â§ sin x, by simpa,
    apply sin_nonneg_of_nonneg_of_le_pi;
    linarith,
  }
end

/-
Use concavity of sin on [0..pi/2] to bound it below.
-/
lemma bound_sin {x : ‚Ñù} (hx1 : 0 ‚â§ x) (hx2 : x ‚â§ real.pi / 2) : 2 / real.pi * x ‚â§ sin x :=
begin
  have h := sin_is_concave.2,
  dsimp at h,
  have pi_pos := pi_pos,
  have pi_nonzero := pi_ne_zero,
  have two_over_pi_pos : (0 :‚Ñù) < (2:‚Ñù) / real.pi := div_pos zero_lt_two pi_pos,
  have hzero : (0:‚Ñù) ‚àà set.Icc 0 (real.pi / 2),
  {
    rw set.mem_Icc,
    split; linarith,
  },
  have hpi2 : real.pi / 2 ‚àà set.Icc 0 (real.pi / 2),
  {
    rw set.mem_Icc,
    split; linarith,
  },
  replace h := h hzero hpi2,
  simp only [sin_zero, mul_one, zero_add, mul_zero, sin_pi_div_two] at h,
  have ha : 0 ‚â§ (1:‚Ñù) - 2 / real.pi * x,
  {
    simp only [sub_nonneg],
    refine (le_div_iff' two_over_pi_pos).mp _,
    simp only [one_div_div],
    exact hx2,
  },
  have hb : 0 ‚â§ 2 / real.pi * x := (zero_le_mul_left two_over_pi_pos).mpr hx1,
  replace h := h ha hb,
  simp only [forall_prop_of_true, sub_add_cancel] at h,
  suffices : 2 / real.pi * x * (real.pi / 2) = x,
  {
    rw this at h,
    exact h,
  },
  discrete_field,
end

lemma key_inequality {n : ‚Ñï} {x : ‚Ñù} (hx1 : 0 ‚â§ x) (hx2 : x ‚â§ real.pi /2) :
  x ^ 2 * cos x ^ (2 * n) ‚â§ (real.pi ^ 2 / 4) ‚Ä¢ (sin x ^ 2 * cos x ^ (2 * n)) :=
begin
  have key := bound_sin hx1 hx2,
  have cospos : (cos x)^(2*n) ‚â• 0,
  {
    rw [mul_comm, pow_mul],
    apply pow_two_nonneg,
  },
  have h : x^2 ‚â§ real.pi^2 / 4 * (sin x)^2,
  {
    rw [div_mul_eq_mul_div, div_le_iff pi_pos] at key,
    nlinarith,
  },
  dsimp,
  nlinarith,
end

lemma BA_aux {n : ‚Ñï} :
  ‚à´ (x : ‚Ñù) in 0..real.pi / 2, x ^ 2 * cos x ^ (2 * n) <
  ‚à´ (x : ‚Ñù) in
    0..real.pi / 2,
    (real.pi ^ 2 / 4) * (sin x ^ 2 * cos x ^ (2 * n)) :=
begin
  have hsq2' : sqrt 2^2 = 2 := sq_sqrt zero_le_two,
  have hsq2 : sqrt 2 ^(2*n) = 2^n := by simp only [pow_mul, hsq2'],
  have pisqpos : 0 < real.pi^2 := pow_pos pi_pos 2,
  apply integral_strictly_monotone_of_cont,
  { show_continuous },
  { show_continuous },
  { exact pi_div_two_pos },
  { apply key_inequality },
  {
    use real.pi/4,
    repeat {split},
    all_goals { try { linarith [pi_pos]}},
    {
      simp only [cos_pi_div_four, sin_pi_div_four, hsq2, hsq2',
        algebra.id.smul_eq_mul, div_pow],
      rw [‚Üêmul_assoc, mul_lt_mul_right],
      all_goals {discrete_field},
    },
  }
end

lemma B_in_terms_of_A (n : ‚Ñï) : B n < real.pi^2 / (8 * (n + 1)) * A n :=
begin      
  have hh := first_lemma_cor n,
  calc
  B n = ‚à´ x in 0..(real.pi/2), x^2 * (cos x)^(2*n) : by {refl}
  ... < ‚à´ x in 0..(real.pi/2), (real.pi^2/ 4) ‚Ä¢ ((sin x)^2 * (cos x)^(2*n)) : by {exact BA_aux}
  ... = (real.pi^2/4) * (A (n+1) / (2*n + 1)) : by {rw [interval_integral.integral_smul,first_lemma'], discrete_field }
  ... = (real.pi^2) / (8 * (n+1)) * (A n) : by {discrete_field}
end

lemma B_in_terms_of_A' (n : ‚Ñï) : 2 * B n / A n < real.pi ^ 2 / (4 *(n + 1)) :=
begin
  have h2 : 0 < (2:‚Ñù) := zero_lt_two,
  calc
  2 * B n / A n = 2 * (B n / A n) : by {exact mul_div_assoc,}
  ... < 2 * (real.pi ^ 2 / (8 * (n + 1))) :
    by {simp only [mul_lt_mul_left h2, div_lt_iff A_pos, B_in_terms_of_A n]}
  ... = real.pi ^ 2 / (4 *(n + 1)) :  by {discrete_field}
end

/-
Bound the partial sums by a harmonic sequence.
-/
lemma error_estimate {n : ‚Ñï}:
  (-real.pi^2/4/(n+1) + real.pi^2/6) ‚â§ (‚àë k in finset.range n, ((1:‚Ñù)/ (k+1)^2))
    ‚àß
  (‚àë k in finset.range n, ((1:‚Ñù)/ (k+1)^2)) ‚â§ real.pi^2/4/(n+1) + real.pi^2/6 :=
begin
  rw [telescoping n, eval_A0, eval_B0],
  have quo_pos : 0 < 2 * B n / A n,
  {
    rw mul_div_assoc,
    exact mul_pos zero_lt_two (div_pos B_pos A_pos),
  },
  have h := B_in_terms_of_A' n,
  have pi_ne_zero := pi_ne_zero,
  have : 2 * (real.pi ^ 3 / 24) / (real.pi / 2) = real.pi^2 / 6 := by {discrete_field},
  rw this,
  field_simp *,
  split,
  all_goals {apply le_of_lt},
  {
    calc (-(real.pi ^ 2 * 6) / (4 * (‚Üën + 1)) + real.pi ^ 2) / 6
      = -(real.pi^2 / (4*((n:‚Ñù) + 1))) + real.pi^2 / 6 : by {discrete_field}
    ... < -(2 * B n / A n) + real.pi^2 / 6 : by {linarith [h]}
    ... = (real.pi ^ 2 - 6 * (2 * B n) / A n) / 6 : by {ring_exp}
  },
  {
    calc (real.pi ^ 2 - 6 * (2 * B n) / A n) / 6
      =  real.pi ^ 2/ 6- (2 * B n / A n): by {discrete_field}
    ... <  real.pi ^ 2 / (4 * (‚Üën + 1)) + real.pi ^ 2 / 6 : by {nlinarith}
    ... = (real.pi ^ 2 * 6 / (4 * (‚Üën + 1)) + real.pi ^ 2) / 6 : by {discrete_field}
  }
end

lemma tendsto_const_div_add_at_top_nhds_0_nat {C : ‚Ñù} :
  tendsto (Œª n : ‚Ñï, (C / ((n : ‚Ñù) + 1))) at_top (ùìù 0) :=
suffices tendsto (Œª n : ‚Ñï, C / (‚Üë(n + 1) : ‚Ñù)) at_top (ùìù 0), by simpa,
(tendsto_add_at_top_iff_nat 1).2 (tendsto_const_div_at_top_nhds_0_nat C)

lemma limit_below : tendsto (Œª (n:‚Ñï),-real.pi^2/4/(n+1) + real.pi^2/6) at_top (ùìù (real.pi^2/6)) :=
begin
  nth_rewrite 0 ‚Üêzero_add (real.pi^2/6),
  apply tendsto.add_const,
  apply tendsto_const_div_add_at_top_nhds_0_nat,
end

lemma limit_above : tendsto (Œª (n:‚Ñï), real.pi^2/4/(n+1) + real.pi^2/6) at_top (ùìù (real.pi^2/6)) :=
begin
  nth_rewrite 0 ‚Üêzero_add (real.pi^2/6),
  apply tendsto.add_const,
  apply tendsto_const_div_add_at_top_nhds_0_nat,
end


theorem euler_summation : tendsto (Œª (n:‚Ñï), (‚àë k in finset.range n, ((1:‚Ñù)/ (k+1)^2))) at_top (nhds (real.pi^2 / 6)) :=
begin
  apply tendsto_of_tendsto_of_tendsto_of_le_of_le limit_below limit_above,
  all_goals {rw pi.le_def, intro n},
  exact error_estimate.1,
  exact error_estimate.2,
end